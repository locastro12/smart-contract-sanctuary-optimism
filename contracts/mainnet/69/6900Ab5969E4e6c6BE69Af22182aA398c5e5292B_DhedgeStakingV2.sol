//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma abicoder v2;

import "../interfaces/IPoolLogic.sol";
import "./interfaces/IDhedgeStakingV2.sol";
import "./Base64.sol";
import "./DhedgeStakingV2VDHTCalculator.sol";
import "./DhedgeStakingV2Storage.sol";
import "./DhedgeStakingV2RewardsCalculator.sol";
import "./DhedgeStakingV2NFTJson.sol";

import "@openzeppelin/contracts-upgradeable/math/SafeMathUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/SafeERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/IERC20Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC721/ERC721Upgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/CountersUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/utils/ReentrancyGuardUpgradeable.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

contract DhedgeStakingV2 is
  IDhedgeStakingV2,
  DhedgeStakingV2Storage,
  DhedgeStakingV2VDHTCalculator,
  DhedgeStakingV2RewardsCalculator,
  ERC721Upgradeable,
  PausableUpgradeable,
  ReentrancyGuardUpgradeable
{
  using SafeMath for uint256;
  using SafeERC20Upgradeable for IERC20Upgradeable;
  using CountersUpgradeable for CountersUpgradeable.Counter;

  event NewStake(uint256 tokenId, uint256 dhtAmount);
  event AddDHTToStake(uint256 tokenId, uint256 dhtAmount);
  event StakePoolTokens(uint256 tokenId, address dhedgePoolAddress, uint256 poolTokenAmount);
  event UnstakePoolTokens(uint256 tokenId, uint256 newTokedId);
  event UnstakeDHT(uint256 tokenId);
  event Claim(uint256 tokenId, uint256 claimAmount);

  function initialize(address _dhtAddress) external initializer {
    __ERC721_init("DhedgeStakingV2", "DHTSV2");
    __Ownable_init();
    __Pausable_init();
    __ReentrancyGuard_init();
    require(_dhtAddress != address(0), "dhtAddress is required");
    dhtAddress = _dhtAddress;
    dhtCap = 1_000_000 * 10**18;
    rewardStreamingTime = 7 days;
    maxVDurationTimeSeconds = 273 days; // 9 Months
    rewardParams = RewardParams({
      stakeDurationDelaySeconds: 30 days,
      maxDurationBoostSeconds: 273 days, // 9 Months
      maxPerformanceBoostNumerator: 500, // 50%
      maxPerformanceBoostDenominator: 1000,
      stakingRatio: 6,
      emissionsRate: 1500,
      emissionsRateDenominator: 1000
    });
  }

  /// @notice implementations should not be left unintialized
  // solhint-disable-next-line no-empty-blocks
  function implInitializer() external initializer {}

  /// OVERRIDE

  /// @notice Stops the transfering of the token that represents a stake
  /// @param from address(0) when minting
  /// @param to address(0) when burning
  function _beforeTokenTransfer(
    address from,
    address to,
    uint256 tokenId
  ) internal virtual override {
    require(to == address(0) || from == address(0), "Token is soulbound");
    super._beforeTokenTransfer(from, to, tokenId);
  }

  /// WRITE External

  /// @notice Create a new stake
  /// @dev User needs to approve this contract for dhtAmount.
  /// @param dhtAmount the amount of dht being staked
  /// @return tokenId the erc721 tokenId
  function newStake(uint256 dhtAmount) external override whenNotPaused nonReentrant returns (uint256 tokenId) {
    if (dhtAmount > 0) {
      IERC20Upgradeable(dhtAddress).safeTransferFrom(msg.sender, address(this), dhtAmount);
    }
    tokenId = _newStake(msg.sender, dhtAmount, block.timestamp);
    _addToGlobalStake(dhtAmount);
    emit NewStake(tokenId, dhtAmount);
  }

  /// @notice Add additional DHT
  /// @dev User needs to approve this contract for DHT first
  /// @param tokenId the erc721 tokenId
  /// @param dhtAmount the amount of dht being staked
  function addDhtToStake(uint256 tokenId, uint256 dhtAmount) external override whenNotPaused nonReentrant {
    require(_isApprovedOrOwner(msg.sender, tokenId), "Must be approved or owner.");
    require(stakes[tokenId].unstaked == false, "Already unstaked");
    require(dhtAmount > 0, "Must add some dht");
    IERC20Upgradeable(dhtAddress).safeTransferFrom(msg.sender, address(this), dhtAmount);
    _addAdditionalDHTToStake(tokenId, dhtAmount);
    _addToGlobalStake(dhtAmount);
    emit AddDHTToStake(tokenId, dhtAmount);
  }

  /// @notice Returns a users staked DHT and if empty burns the nft
  /// @dev This should only be called on an empty stake (i.e no pooltokens staked), otherwise can miss out on rewards
  /// @param tokenId the tokenId that represents the Stake
  function unstakeDHT(uint256 tokenId, uint256 dhtAmount) external override whenNotPaused nonReentrant {
    require(_isApprovedOrOwner(msg.sender, tokenId), "Must be approved or owner.");
    IDhedgeStakingV2Storage.Stake storage stake = stakes[tokenId];
    require(stake.dhtAmount >= dhtAmount, "Not enough staked dht.");
    uint256 vDHTBefore = vDHTBalanceOfStake(tokenId);
    stake.dhtAmount = stake.dhtAmount.sub(dhtAmount);
    uint256 vDHTAfter = vDHTBalanceOfStake(tokenId);
    _removeFromGlobalStake(dhtAmount, vDHTBefore.sub(vDHTAfter));
    IERC20Upgradeable(dhtAddress).safeTransfer(msg.sender, dhtAmount);
    emit UnstakeDHT(tokenId);
  }

  /// @param dhedgePoolAddress the address of pool that is being staked
  /// @param dhedgePoolAmount Amount of Pool tokens being staked
  function stakePoolTokens(
    uint256 tokenId,
    address dhedgePoolAddress,
    uint256 dhedgePoolAmount
  ) external override whenNotPaused nonReentrant {
    require(_isApprovedOrOwner(msg.sender, tokenId), "Must be approved or owner.");
    _checkPoolConfigured(dhedgePoolAddress);
    IDhedgeStakingV2Storage.Stake storage stake = stakes[tokenId];
    require(stake.unstaked == false, "Already unstaked");
    require(stake.dhedgePoolAddress == address(0), "Pool Tokens already staked.");

    IERC20Upgradeable(dhedgePoolAddress).safeTransferFrom(msg.sender, address(this), dhedgePoolAmount);

    uint256 tokenPrice = IPoolLogic(dhedgePoolAddress).tokenPrice();
    uint256 totalDepositValue = tokenPrice.mul(dhedgePoolAmount).div(10**18);
    poolConfiguration[dhedgePoolAddress].stakedSoFar = poolConfiguration[dhedgePoolAddress].stakedSoFar.add(
      totalDepositValue
    );
    _checkPoolCap(dhedgePoolAddress);

    // Not sure if we still want to have the global limit in addition to per pool limits
    totalStakingValue = totalStakingValue.add(totalDepositValue);
    _checkMaxStaking();

    stake.dhedgePoolAddress = dhedgePoolAddress;
    stake.dhedgePoolAmount = dhedgePoolAmount;
    stake.stakeStartTokenPrice = tokenPrice;
    stake.dhedgePoolStakeStartTime = block.timestamp;
    // The staking contract is a whitelisted receiver and therefore it should enforce the token lockup
    stake.dhedgePoolRemainingExitCooldownAtStakeTime = IPoolLogic(dhedgePoolAddress).getExitRemainingCooldown(
      msg.sender
    );
    emit StakePoolTokens(tokenId, stake.dhedgePoolAddress, stake.dhedgePoolAmount);
  }

  /// @notice Allows the user to unstake their dhedge pool tokens, when called will be allocated rewards at this point.
  /// @dev Once the user unstakes their pooltokens the rewards to be recieved are calculated and assigned to the user. This stake is retired.
  /// @dev DHT is automatically zapped to a new stake that maintains it's existing vDHT. A user can then stake new pool tokens against these dht, or unstake the DHT.
  /// @param tokenId The erc721 id of the existing stake
  /// @return newTokenId the tokenId where the dht were zapped to.
  function unstakePoolTokens(uint256 tokenId)
    external
    override
    whenNotPaused
    nonReentrant
    returns (uint256 newTokenId)
  {
    require(_isApprovedOrOwner(msg.sender, tokenId), "Must be approved or owner.");
    IDhedgeStakingV2Storage.Stake storage stake = stakes[tokenId];
    require(
      stake.dhedgePoolStakeStartTime.add(stake.dhedgePoolRemainingExitCooldownAtStakeTime) < block.timestamp,
      "cooldown active"
    );
    require(stake.unstaked == false, "Already unstaked");
    require(stake.dhedgePoolAddress != address(0), "No pool tokens staked");
    stake.unstaked = true;
    stake.unstakeTime = block.timestamp;

    uint256 tokenPriceFinish = IPoolLogic(stake.dhedgePoolAddress).tokenPrice();
    stake.stakeFinishTokenPrice = tokenPriceFinish;
    uint256 vhdt = vDHTBalanceOfStake(tokenId);
    stake.vdhtAccruedAtUnstake = vhdt;
    // Staker only receives rewards if they've staked for minimum stake time
    if (block.timestamp.sub(stake.dhedgePoolStakeStartTime) >= rewardParams.stakeDurationDelaySeconds) {
      stake.reward = calculateDhtRewardAmount(
        vhdt,
        stake.dhedgePoolAmount,
        stake.stakeStartTokenPrice,
        tokenPriceFinish,
        stake.dhedgePoolStakeStartTime,
        block.timestamp,
        stake.rewardParamsEmissionsRate,
        rewardParams
      );
    }

    uint256 stakedDhtAmount = stake.dhtAmount;
    stake.dhtAmount = 0;
    // Once pool tokens have been claimed we teleport the staked DHT to a new Stake
    // The existing stake is used for claiming the rewards
    newTokenId = _newStake(ownerOf(tokenId), stakedDhtAmount, stake.dhtStakeStartTime);
    uint256 originalValueStaked = stake.stakeStartTokenPrice.mul(stake.dhedgePoolAmount).div(10**18);
    poolConfiguration[stake.dhedgePoolAddress].stakedSoFar = poolConfiguration[stake.dhedgePoolAddress].stakedSoFar.sub(
      originalValueStaked
    );

    totalStakingValue = totalStakingValue.sub(originalValueStaked);
    dhtRewarded = dhtRewarded.add(stake.reward);

    IERC20Upgradeable(stake.dhedgePoolAddress).safeTransfer(msg.sender, stake.dhedgePoolAmount);
    emit UnstakePoolTokens(tokenId, newTokenId);
  }

  /// @notice Used to claim rewards for an unstaked position
  /// @dev The user can claim all rewards once the rewardStreamingTime has passed or a pro-rate amount
  /// @param tokenId the tokenId that represents the Stake that has been unstaked
  function claim(uint256 tokenId) external override nonReentrant {
    require(_isApprovedOrOwner(msg.sender, tokenId), "Must be approved or owner.");
    IDhedgeStakingV2Storage.Stake storage stake = stakes[tokenId];
    require(stake.unstaked == true, "Not Unstaked.");
    uint256 claimAmount = canClaimAmount(tokenId);
    require(claimAmount > 0, "Nothing to claim");
    checkEnoughDht(claimAmount);
    stake.claimedReward = stake.claimedReward.add(claimAmount);

    require(stake.claimedReward <= stake.reward, "Claiming to much.");
    IERC20Upgradeable(dhtAddress).safeTransfer(msg.sender, claimAmount);
    emit Claim(tokenId, claimAmount);
  }

  /// WRITE Internal

  function _addToGlobalStake(uint256 dhtAmount) internal {
    uint256 target = globalVDHT();
    dhtStaked = dhtStaked.add(dhtAmount);
    uint256 unit = 10**18;
    if (dhtStaked > 0) {
      aggregateStakeStartTime = block.timestamp.sub(
        maxVDurationTimeSeconds.mul(target).mul(unit).div(dhtStaked).div(unit)
      );
    } else {
      aggregateStakeStartTime = 0;
    }
  }

  function _removeFromGlobalStake(uint256 dhtAmount, uint256 vDHTAccrued) internal {
    require(dhtAmount >= vDHTAccrued, "dhtAmount must be => vDHTAccrued");
    uint256 target = globalVDHT().sub(vDHTAccrued);
    dhtStaked = dhtStaked.sub(dhtAmount);
    uint256 unit = 10**18;
    if (dhtStaked == 0) {
      aggregateStakeStartTime = block.timestamp;
    } else {
      aggregateStakeStartTime = block.timestamp.sub(
        maxVDurationTimeSeconds.mul(target).mul(unit).div(dhtStaked).div(unit)
      );
    }
  }

  function globalVDHT() public view returns (uint256) {
    return calculateVDHT(aggregateStakeStartTime, dhtStaked, block.timestamp, maxVDurationTimeSeconds);
  }

  /// @notice Adjust an existing stake by the additionalDHT and reduces the stake time so vDHT remains consistent
  /// @dev this allows additional DHT to be staked, but prevents manipulation of rewards, cannot stake dht to unstaked/claimed staked
  /// @param tokenId the erc721 tokenId that represents the stake
  /// @param additionalDHT the additional amount of dht being staked
  function _addAdditionalDHTToStake(uint256 tokenId, uint256 additionalDHT) internal {
    require(additionalDHT > 0, "Must stake some additional dht.");
    IDhedgeStakingV2Storage.Stake storage stake = stakes[tokenId];
    require(stake.unstaked == false, "Already unstaked.");
    uint256 unit = 10**18;
    uint256 timePassed = block.timestamp - stake.dhtStakeStartTime;
    uint256 currentlyStaked = stake.dhtAmount;
    uint256 newStakeAmount = stake.dhtAmount.add(additionalDHT);
    // now - (((now - startTime)) * ((amountStaked * unit) /(additionalAmount + amountStaked))) / unit
    uint256 adjustment = timePassed.mul(currentlyStaked.mul(unit).div(newStakeAmount)).div(unit);
    stake.dhtStakeStartTime = block.timestamp.sub(adjustment);
    stake.dhtAmount = newStakeAmount;
  }

  /// @notice Creates a new DHT stake for the user with the given parameters
  /// @dev Must remain internal
  /// @param owner The owner of the stake to be created
  /// @param dhtAmount the amount of dht for the new stake
  /// @param stakeStartTime the time the stake should start from
  function _newStake(
    address owner,
    uint256 dhtAmount,
    uint256 stakeStartTime
  ) internal returns (uint256 newTokenId) {
    newTokenId = _mint(owner);
    stakes[newTokenId] = IDhedgeStakingV2Storage.Stake({
      dhtAmount: dhtAmount,
      dhtStakeStartTime: stakeStartTime,
      dhedgePoolAddress: address(0),
      dhedgePoolAmount: 0,
      dhedgePoolStakeStartTime: 0,
      dhedgePoolRemainingExitCooldownAtStakeTime: 0,
      stakeStartTokenPrice: 0,
      unstaked: false,
      unstakeTime: 0,
      reward: 0,
      claimedReward: 0,
      rewardParamsEmissionsRate: rewardParams.emissionsRate,
      stakeFinishTokenPrice: 0,
      vdhtAccruedAtUnstake: 0
    });
  }

  /// @notice Handles incrementing the tokenIdCounter and minting the nft
  /// @param to the stakers address
  function _mint(address to) internal returns (uint256 tokenId) {
    tokenId = _tokenIdCounter.current();
    _safeMint(to, tokenId);
    _tokenIdCounter.increment();
  }

  /// VIEW

  /// @notice Allows getting stake info
  /// @param tokenId the erc721 id of the stake
  /// @return stake the stake struct for the given tokenID
  function getStake(uint256 tokenId) external view override returns (Stake memory) {
    return stakes[tokenId];
  }

  /// @notice Calculates the max amount of DHPT value that can be currently staked
  /// @dev N.B this does not enforce the ratio the users stake at. It simply caps emissions under the circumstances where every staker stakes at the optimal ratio and maxes out their rewards
  /// @return maximumStakingValue The max amount of DHPT value that should currently be staked
  function maxStakingValue() public view returns (uint256 maximumStakingValue) {
    maximumStakingValue = dhtCap
      .sub(dhtRewarded)
      .div(rewardParams.stakingRatio)
      .mul(rewardParams.emissionsRateDenominator)
      .div(rewardParams.emissionsRate);
  }

  /// @notice Allows getting configuration of a pool
  /// @param dhedgePoolAddress the dhedge pool address to get the configuration for
  function getPoolConfiguration(address dhedgePoolAddress)
    external
    view
    override
    returns (IDhedgeStakingV2Storage.PoolConfiguration memory)
  {
    return poolConfiguration[dhedgePoolAddress];
  }

  /// @notice Returns the token holder amount can claim based on the time passed since they unstaked
  /// @dev The user can only claim a proportional amount until `rewardStreamingTime` has passed
  /// @param tokenId the tokenId that represents the Stake that has been unstaked
  /// @return claimAmount the amount the staker can claim
  function canClaimAmount(uint256 tokenId) public view override returns (uint256 claimAmount) {
    IDhedgeStakingV2Storage.Stake memory stake = stakes[tokenId];
    if (stake.unstakeTime == 0) {
      return 0;
    }
    uint256 rewardsPerSecond = stake.reward.div(rewardStreamingTime);
    uint256 timePassed = block.timestamp.sub(stake.unstakeTime);
    if (timePassed > rewardStreamingTime) {
      claimAmount = stake.reward.sub(stake.claimedReward);
    } else {
      uint256 canStream = rewardsPerSecond.mul(timePassed);
      claimAmount = canStream.sub(stake.claimedReward);
    }
  }

  /// @notice Returns the current vDHT of an address
  /// @dev this changes every block based on the time passed since staking
  /// @param staker the stakers address
  /// @return vDHT the current aggregate vDHT for the staker
  function vDHTBalanceOf(address staker) external view override returns (uint256 vDHT) {
    uint256 balance = balanceOf(staker);
    for (uint256 i = 0; i < balance; i++) {
      uint256 tokenId = tokenOfOwnerByIndex(staker, i);
      vDHT = vDHT.add(vDHTBalanceOfStake(tokenId));
    }
  }

  /// @notice Returns the current vDHT of a stake
  /// @dev this changes every block based on the time passed since staking
  /// @param tokenId the id of the stake
  /// @return vDHT the current vDHT for the given stake
  function vDHTBalanceOfStake(uint256 tokenId) public view override returns (uint256 vDHT) {
    IDhedgeStakingV2Storage.Stake memory stake = stakes[tokenId];
    if (stake.dhtAmount > 0) {
      vDHT = calculateVDHT(stake.dhtStakeStartTime, stake.dhtAmount, block.timestamp, maxVDurationTimeSeconds);
    }
  }

  /// @notice Returns the aggregate DHT staked of an address
  /// @param staker the stakers address
  /// @return dht the current aggregate DHT for the address
  function dhtBalanceOf(address staker) external view override returns (uint256 dht) {
    uint256 balance = balanceOf(staker);
    for (uint256 i = 0; i < balance; i++) {
      uint256 tokenId = tokenOfOwnerByIndex(staker, i);
      dht = dht.add(stakes[tokenId].dhtAmount);
    }
  }

  /// @notice The rewards a stake would receive if unstaked now
  /// @param tokenId the id of the stake
  /// @return rewardsDHT the current aggregate DHT for the address
  function currentRewardsForStake(uint256 tokenId) public view override returns (uint256 rewardsDHT) {
    IDhedgeStakingV2Storage.Stake memory stake = stakes[tokenId];
    if (stake.dhedgePoolAddress != address(0)) {
      uint256 currentTokenPrice = IPoolLogic(stake.dhedgePoolAddress).tokenPrice();
      rewardsDHT = calculateDhtRewardAmount(
        vDHTBalanceOfStake(tokenId),
        stake.dhedgePoolAmount,
        stake.stakeStartTokenPrice,
        currentTokenPrice,
        stake.dhedgePoolStakeStartTime,
        block.timestamp,
        stake.rewardParamsEmissionsRate,
        rewardParams
      );
    } else {
      rewardsDHT = 0;
    }
  }

  function tokenURI(uint256 tokenId) public view override returns (string memory) {
    IDhedgeStakingV2Storage.Stake memory stake = stakes[tokenId];
    string memory poolSymbol;
    uint256 currentTokenPrice;

    if (stake.dhedgePoolAddress != address(0)) {
      poolSymbol = IPoolLogic(stake.dhedgePoolAddress).symbol();
      currentTokenPrice = IPoolLogic(stake.dhedgePoolAddress).tokenPrice();
    }

    return
      tokenUriGenerator.tokenJson(
        tokenId,
        stake,
        vDHTBalanceOfStake(tokenId),
        currentRewardsForStake(tokenId),
        poolSymbol,
        currentTokenPrice,
        dhtAddress,
        ownerOf(tokenId)
      );
  }

  /// ASSERT

  /// @notice Check the dhedge pool being staked is on the allow list
  /// @param dhedgePoolAddress address of the dhedge pool
  function _checkPoolConfigured(address dhedgePoolAddress) internal view {
    require(poolConfiguration[dhedgePoolAddress].configured == true, "Pool not allowed.");
  }

  /// @notice Check the amount being staked doesnt exceed the pools configured cap
  /// @param dhedgePoolAddress address of the dhedge pool
  function _checkPoolCap(address dhedgePoolAddress) internal view {
    PoolConfiguration memory pc = poolConfiguration[dhedgePoolAddress];
    require(pc.stakedSoFar <= pc.stakeCap, "Cap for pool will be exceeded.");
  }

  /// @notice Check here that the amount of value being staked isn't more than the max
  /// @dev This can help control emissions
  function _checkMaxStaking() internal view {
    require(totalStakingValue <= maxStakingValue(), "Staking cap will be exceeded.");
  }

  /// @notice Check here we don't distribute any staked DHT as rewards
  /// @param claimAmount the amount of dht attempting to be claimed
  function checkEnoughDht(uint256 claimAmount) public view {
    uint256 balanceNotStaked = IERC20Upgradeable(dhtAddress).balanceOf(address(this)).sub(dhtStaked);
    require(balanceNotStaked >= claimAmount, "Rewards depleted.");
  }
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;

interface IPoolLogic {
  function factory() external view returns (address);

  function poolManagerLogic() external view returns (address);

  function setPoolManagerLogic(address _poolManagerLogic) external returns (bool);

  function availableManagerFee() external view returns (uint256 fee);

  function tokenPrice() external view returns (uint256 price);

  function tokenPriceWithoutManagerFee() external view returns (uint256 price);

  function mintManagerFee() external;

  function deposit(address _asset, uint256 _amount) external returns (uint256 liquidityMinted);

  function depositFor(
    address _recipient,
    address _asset,
    uint256 _amount
  ) external returns (uint256 liquidityMinted);

  function depositForWithCustomCooldown(
    address _recipient,
    address _asset,
    uint256 _amount,
    uint256 _cooldown
  ) external returns (uint256 liquidityMinted);

  function withdraw(uint256 _fundTokenAmount) external;

  function transfer(address to, uint256 value) external returns (bool);

  function balanceOf(address owner) external view returns (uint256);

  function approve(address spender, uint256 amount) external returns (bool);

  function symbol() external view returns (string memory);

  function transferFrom(
    address from,
    address to,
    uint256 value
  ) external returns (bool);

  function getExitRemainingCooldown(address sender) external view returns (uint256 remaining);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.5.0) (utils/Base64.sol)

pragma solidity 0.7.6;
pragma abicoder v2;

/**
 * @dev Provides a set of functions to operate with Base64 strings.
 *
 * _Available since v4.5._
 */
library Base64 {
  /**
   * @dev Base64 Encoding/Decoding Table
   */
  string internal constant _TABLE = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/";

  /**
   * @dev Converts a `bytes` to its Bytes64 `string` representation.
   */
  function encode(bytes memory data) internal pure returns (string memory) {
    /**
     * Inspired by Brecht Devos (Brechtpd) implementation - MIT licence
     * https://github.com/Brechtpd/base64/blob/e78d9fd951e7b0977ddca77d92dc85183770daf4/base64.sol
     */
    if (data.length == 0) return "";

    // Loads the table into memory
    string memory table = _TABLE;

    // Encoding takes 3 bytes chunks of binary data from `bytes` data parameter
    // and split into 4 numbers of 6 bits.
    // The final Base64 length should be `bytes` data length multiplied by 4/3 rounded up
    // - `data.length + 2`  -> Round up
    // - `/ 3`              -> Number of 3-bytes chunks
    // - `4 *`              -> 4 characters for each chunk
    string memory result = new string(4 * ((data.length + 2) / 3));

    assembly {
      // Prepare the lookup table (skip the first "length" byte)
      let tablePtr := add(table, 1)

      // Prepare result pointer, jump over length
      let resultPtr := add(result, 32)

      // Run over the input, 3 bytes at a time
      for {
        let dataPtr := data
        let endPtr := add(data, mload(data))
        // solhint-disable-next-line no-empty-blocks
      } lt(dataPtr, endPtr) {

      } {
        // Advance 3 bytes
        dataPtr := add(dataPtr, 3)
        let input := mload(dataPtr)

        // To write each character, shift the 3 bytes (18 bits) chunk
        // 4 times in blocks of 6 bits for each character (18, 12, 6, 0)
        // and apply logical AND with 0x3F which is the number of
        // the previous character in the ASCII table prior to the Base64 Table
        // The result is then added to the table to get the character to write,
        // and finally write it in the result pointer but with a left shift
        // of 256 (1 byte) - 8 (1 ASCII char) = 248 bits

        mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))
        resultPtr := add(resultPtr, 1) // Advance

        mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))
        resultPtr := add(resultPtr, 1) // Advance

        mstore8(resultPtr, mload(add(tablePtr, and(shr(6, input), 0x3F))))
        resultPtr := add(resultPtr, 1) // Advance

        mstore8(resultPtr, mload(add(tablePtr, and(input, 0x3F))))
        resultPtr := add(resultPtr, 1) // Advance
      }

      // When data `bytes` is not exactly 3 bytes long
      // it is padded with `=` characters at the end
      switch mod(mload(data), 3)
      case 1 {
        mstore8(sub(resultPtr, 1), 0x3d)
        mstore8(sub(resultPtr, 2), 0x3d)
      }
      case 2 {
        mstore8(sub(resultPtr, 1), 0x3d)
      }
    }

    return result;
  }
}

//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

import "@openzeppelin/contracts/math/SafeMath.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

pragma solidity 0.7.6;

/**
 * @title A library for Calculating VDHT for a stake.
 */
contract DhedgeStakingV2VDHTCalculator {
  using SafeMath for uint256;

  /// @notice Calculates the amount of vDHT based on the amount staked and the amount of time thats passed since staking
  /// @param stakeStartTime when the dht started being staked
  /// @param dhtAmount the amount of dht being staked
  /// @return vDHT the current vDHT
  function calculateVDHT(
    uint256 stakeStartTime,
    uint256 dhtAmount,
    uint256 stakeFinishTime,
    uint256 maxVDurationTime
  ) public pure returns (uint256 vDHT) {
    uint256 secondsPast = stakeFinishTime.sub(stakeStartTime);
    if (secondsPast > maxVDurationTime) {
      vDHT = dhtAmount;
    } else {
      vDHT = dhtAmount.mul(secondsPast).div(maxVDurationTime);
    }
  }
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma abicoder v2;

import "./Base64.sol";
import "./interfaces/IDhedgeStakingV2Storage.sol";

import "@openzeppelin/contracts/math/SafeMath.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

// This is a standalone contract responsible for generating the token metadata and image encoded as base64 json
// It is a seperate contract because of the contract size restriction of 24kb
contract DhedgeStakingV2NFTJson {
  using SafeMath for uint256;

  /// @notice Generates the tokenUri base64 including the svg
  /// @param tokenId the erc721 tokenId
  /// @param stake the stake struct
  /// @param vDHT the amount of accrued vdht for the stake
  /// @param rewards the amount of rewards accrued for the stake
  /// @param poolSymbol the symbol of the staked pool tokens
  /// @param currentTokenPrice the price of the pool tokens staked
  /// @param dhtAddress the address of dht
  /// @param owner the owner of the stake
  /// @return tokenJson base64 encoded json payload
  function tokenJson(
    uint256 tokenId,
    IDhedgeStakingV2Storage.Stake memory stake,
    uint256 vDHT,
    uint256 rewards,
    string memory poolSymbol,
    uint256 currentTokenPrice,
    address dhtAddress,
    address owner
  ) public view returns (string memory) {
    string memory svgData = getSvg(stake, vDHT, poolSymbol, currentTokenPrice, rewards, dhtAddress, owner);
    currentTokenPrice;
    // Need to add pool token information here
    string memory json = Base64.encode(
      // solhint-disable quotes
      bytes(
        string(
          abi.encodePacked(
            "{",
            '"name": "DHT Stake: ',
            Strings.toString(tokenId),
            '",',
            '"description": "vDHT Accruing DHT stake",',
            '"image_data": "',
            bytes(svgData),
            '",',
            '"attributes": ',
            "[",
            '{"trait_type": "Staked DHT", "value":',
            Strings.toString(stake.dhtAmount),
            " },",
            '{"trait_type": "vDHT", "value":',
            Strings.toString(vDHT),
            "}",
            "]",
            "}"
          )
        )
      )
      // solhint-enable quotes
    );
    return string(abi.encodePacked("data:application/json;base64,", json));
  }

  /// @notice Generates svg string (not encoded)
  /// @param stake the stake struct
  /// @param vDHT the amount of accrued vdht for the stake
  /// @param rewards the amount of rewards accrued for the stake
  /// @param poolSymbol the symbol of the staked pool tokens
  /// @param currentTokenPrice the price of the pool tokens staked
  /// @param dhtAddress the address of dht
  /// @param owner the owner of the stake
  /// @return svg unencoded svg
  function getSvg(
    IDhedgeStakingV2Storage.Stake memory stake,
    uint256 vDHT,
    string memory poolSymbol,
    uint256 currentTokenPrice,
    uint256 rewards,
    address dhtAddress,
    address owner
  ) internal view returns (string memory) {
    string[3] memory mainParts;
    //<stop offset='1e-05' stop-color='#ff918f' stop-opacity='1'/><stop offset='1' stop-color='#ff0066' stop-opacity='1'/>
    mainParts[
      0
    ] = "<?xml version='1.0' encoding='UTF-8'?> <svg width='599' height='844' font-family='Arial' viewBox='0 0 599 844' xmlns='http://www.w3.org/2000/svg' xmlns:xlink='http://www.w3.org/1999/xlink'>";
    string memory guts;

    // We split these up because of stack to deep
    {
      string memory ownerStr = addressToString(owner);
      uint256 ownerLength = bytes(ownerStr).length;
      string memory poolStr = addressToString(stake.dhedgePoolAddress);
      uint256 poolLength = bytes(poolStr).length;
      string memory color1 = string(
        abi.encodePacked("#", substring(ownerStr, ownerLength.sub(2), ownerLength), "a0d0")
      );
      string memory color2 = string(abi.encodePacked("#", substring(poolStr, poolLength.sub(2), poolLength), "5093"));
      guts = string(
        abi.encodePacked(
          "<g id='Group'>"
          "<linearGradient id='linearGradient1' x1='14.8' y1='14.5' x2='584.4' y2='14.5' gradientUnits='userSpaceOnUse'>"
          "<stop offset='1e-05' stop-color='",
          color1,
          "' stop-opacity='1'/>"
          "<stop offset='1' stop-color='",
          color2,
          "' stop-opacity='1'/>"
          "</linearGradient>"
          "<path id='Path' fill='url(#linearGradient1)' stroke='none' d='M 40.100006 828.700012 C 26.199997 828.700012 14.800003 817.400024 14.800003 803.400024 L 14.800003 39.799988 C 14.800003 25.900024 26.100006 14.5 40.100006 14.5 L 559.099976 14.5 C 573 14.5 584.400024 25.799988 584.400024 39.799988 L 584.400024 803.5 C 584.400024 817.400024 573.099976 828.799988 559.099976 828.799988 L 40.100006 828.799988 Z'/>"
          "<path id='path1' fill='url(#linearGradient1)' stroke='none' d='M 559.099976 29 C 565 29 569.900024 33.799988 569.900024 39.799988 L 569.900024 803.5 C 569.900024 809.400024 565.099976 814.299988 559.099976 814.299988 L 40.100006 814.299988 C 34.199997 814.299988 29.300003 809.5 29.300003 803.5 L 29.300003 39.799988 C 29.300003 33.900024 34.100006 29 40.100006 29 L 559.099976 29 M 559.099976 0 L 40.100006 0 C 18.100006 0 0.300003 17.799988 0.300003 39.799988 L 0.300003 803.5 C 0.300003 825.5 18.100006 843.299988 40.100006 843.299988 L 559.099976 843.299988 C 581.099976 843.299988 598.900024 825.5 598.900024 803.5 L 598.900024 39.799988 C 598.900024 17.799988 581.099976 0 559.099976 0 L 559.099976 0 Z'/> </g> <path id='path2' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 235 227.799988 C 235 275.848755 196.048767 314.799988 148 314.799988 C 99.951233 314.799988 61 275.848755 61 227.799988 C 61 179.751221 99.951233 140.799988 148 140.799988 C 196.048767 140.799988 235 179.751221 235 227.799988 Z'/> <g id='g1'> <path id='path3' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 235 227.799988 C 235 275.848755 196.048767 314.799988 148 314.799988 C 99.951233 314.799988 61 275.848755 61 227.799988 C 61 179.751221 99.951233 140.799988 148 140.799988 C 196.048767 140.799988 235 179.751221 235 227.799988 Z'/> <path id='path4' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 118.100006 146 C 118.100006 146 64.100006 212.099976 109.700012 305.700012'/> <path id='path5' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 149 140.799988 L 146.299988 314.799988'/> <path id='path6' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 177.200012 309.299988 C 177.200012 309.299988 231.200012 243.200012 185.600006 149.599976'/> <path id='path7' fill='none' stroke='#000000' stroke-width='2' stroke-linecap='round' stroke-linejoin='round' d='M 227.899994 191.799988 C 227.899994 191.799988 158.899994 141.5 67.899994 192.299988'/> <path id='path8' fill='none' stroke='#000000' stroke-width='2.0158' stroke-linecap='round' stroke-linejoin='round' d='M 67.299988 260 C 67.299988 260 137.399994 310.200012 229.799988 259.299988'/> <path id='path9' fill='none' stroke='#000000' stroke-width='1.4931' stroke-linecap='round' stroke-linejoin='round' d='M 232.399994 212 C 232.399994 212 159.299988 185.599976 62.899994 212.299988'/> <path id='path10' fill='none' stroke='#000000' stroke-width='1.4931' stroke-linecap='round' stroke-linejoin='round' d='M 63.100006 247.799988 C 63.100006 247.799988 136.200012 274.200012 232.600006 247.5'/> <path id='path11' fill='none' stroke='#000000' stroke-width='2.9743' stroke-linecap='round' stroke-linejoin='round' d='M 568.400024 717.900024 L 204.5 296.099976'/> </g> <g id='g2'> <path id='path12' fill='#000000' stroke='none' d='M 88.899994 779.099976 C 88.899994 778.599976 88.899994 778.200012 88.899994 777.799988 C 88.899994 760.900024 88.899994 744 88.899994 727.099976 C 88.899994 726.099976 89.200012 725.700012 90.100006 725.299988 C 93.399994 724 96.600006 722.599976 99.899994 721.200012 C 100.200012 721.099976 100.5 721 101 720.799988 C 101 721.200012 101 721.599976 101 721.900024 C 101 739 101 756.200012 101 773.299988 C 101 774.099976 100.799988 774.5 100 774.799988 C 96.700012 776 93.399994 777.299988 90.100006 778.599976 C 89.700012 778.799988 89.399994 779 88.899994 779.099976 Z'/> <path id='path13' fill='#000000' stroke='none' d='M 122.399994 783.5 C 119.399994 782 116.5 780.5 113.600006 779.099976 C 113.600006 779.099976 113.5 779.099976 113.5 779.099976 C 110.799988 777.5 110.600006 776.799988 110.600006 774.400024 C 110.700012 764.700012 110.700012 755 110.700012 745.299988 C 110.700012 744.900024 110.700012 744.5 110.700012 743.900024 C 111.799988 744.400024 112.899994 744.799988 113.899994 745.299988 C 116.399994 746.400024 119 747.599976 121.5 748.700012 C 122 748.900024 122.5 749.200012 122.399994 749.900024 C 122.399994 760.799988 122.399994 771.700012 122.399994 782.700012 C 122.399994 782.900024 122.399994 783.099976 122.399994 783.5 Z'/> <path id='path14' fill='#000000' stroke='none' d='M 67.200012 787.900024 C 67.200012 787.400024 67.200012 787 67.200012 786.700012 C 67.200012 778 67.200012 769.200012 67.200012 760.5 C 67.200012 759.700012 67.399994 759.299988 68.200012 759 C 71.600006 757.5 75 755.900024 78.399994 754.400024 C 78.5 754.299988 78.700012 754.299988 79 754.200012 C 79 754.599976 79 754.900024 79 755.299988 C 79 764.200012 79 773.200012 79 782.099976 C 79 782.900024 78.799988 783.200012 78.100006 783.5 C 74.700012 785 71.299988 786.400024 67.899994 787.900024 C 67.799988 787.900024 67.600006 787.900024 67.200012 787.900024 Z'/> </g> <path id='path15' fill='none' stroke='#000000' stroke-width='0.9801' stroke-linecap='round' stroke-linejoin='round' d='M 296.899994 140.799988 C 296.899994 164.382568 277.782562 183.5 254.200012 183.5 C 230.617432 183.5 211.5 164.382568 211.5 140.799988 C 211.5 117.217407 230.617432 98.099976 254.200012 98.099976 C 277.782562 98.099976 296.899994 117.217407 296.899994 140.799988 Z'/> <g id='g3'> <path id='path16' fill='none' stroke='#000000' stroke-width='0.9801' stroke-linecap='round' stroke-linejoin='round' d='M 296.899994 140.799988 C 296.899994 164.382568 277.782562 183.5 254.200012 183.5 C 230.617432 183.5 211.5 164.382568 211.5 140.799988 C 211.5 117.217407 230.617432 98.099976 254.200012 98.099976 C 277.782562 98.099976 296.899994 117.217407 296.899994 140.799988 Z'/> <path id='path17' fill='none' stroke='#000000' stroke-width='0.9991' stroke-linecap='round' stroke-linejoin='round' d='M 238.5 99.900024 C 238.5 99.900024 211.5 132.900024 234.299988 179.700012'/> <path id='path18' fill='none' stroke='#000000' stroke-width='0.9801' stroke-linecap='round' stroke-linejoin='round' d='M 254.700012 98.099976 L 253.399994 183.400024'/> <path id='path19' fill='none' stroke='#000000' stroke-width='0.9991' stroke-linecap='round' stroke-linejoin='round' d='M 268 181.5 C 268 181.5 295 148.5 272.200012 101.700012'/> <path id='path20' fill='none' stroke='#000000' stroke-width='0.9801' stroke-linecap='round' stroke-linejoin='round' d='M 293.399994 123.099976 C 293.399994 123.099976 259.600006 98.5 215 123.400024'/> <path id='path21' fill='none' stroke='#000000' stroke-width='0.9879' stroke-linecap='round' stroke-linejoin='round' d='M 214.700012 156.599976 C 214.700012 156.599976 249 181.200012 294.299988 156.299988'/> <path id='path22' fill='none' stroke='#000000' stroke-width='0.7344' stroke-linecap='round' stroke-linejoin='round' d='M 295 132.799988 C 295 132.799988 259 119.799988 211.600006 132.900024'/> <path id='path23' fill='none' stroke='#000000' stroke-width='0.7317' stroke-linecap='round' stroke-linejoin='round' d='M 212.600006 150.599976 C 212.600006 150.599976 248.399994 163.599976 295.700012 150.5'/>"
          "</g>"
        )
      );
    }

    {
      if (stake.dhedgePoolAddress != address(0)) {
        uint256 poolAmount = stake.dhedgePoolAmount;
        // div(10**36) would give us flat dollars, so we div(10**34) and then take the last two digits as decimals
        uint256 amount = poolAmount.mul(currentTokenPrice).div(10**34);
        string memory amountStr = Strings.toString(amount);
        uint256 length = bytes(amountStr).length;
        string memory dollars = length > 2 ? substring(amountStr, 0, length.sub(2)) : "0";
        string memory cents = length < 2
          ? string(abi.encodePacked("0", amountStr))
          : substring(amountStr, length.sub(2), length);

        guts = string(
          abi.encodePacked(
            guts,
            "<text id='ETHBull3x---' xml:space='preserve'><tspan x='64' y='457' font-size='28' fill='#000000' xml:space='preserve'>",
            poolSymbol,
            "</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>",
            "<text id='TVL' xml:space='preserve'><tspan x='64' y='495' font-size='32' font-weight='700' fill='#000000' xml:space='preserve'>$",
            dollars,
            ".",
            cents,
            "</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>"
          )
        );
      }
    }

    // We split these up because of stack to deep
    guts = string(
      abi.encodePacked(
        guts,
        "<text id='DHT-TIME-STAKED-' xml:space='preserve'><tspan x='265' y='229' font-size='24' fill='#000000' xml:space='preserve'>DHT TIME STAKED:</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>",
        "<text id='4-Days--' xml:space='preserve'><tspan x='265' y='272' font-size='32' font-weight='700' fill='#000000' xml:space='preserve'>",
        Strings.toString(block.timestamp.sub(stake.dhtStakeStartTime).div(1 days)),
        " Days</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>"
      )
    );

    guts = string(
      abi.encodePacked(
        guts,
        "<text id='DHTVDHT---' xml:space='preserve'><tspan x='64' y='547' font-size='28' fill='#000000' xml:space='preserve'>DHT:VDHT</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>"
        "<text id='dht:vdht' xml:space='preserve'><tspan x='64' y='585' font-size='32' font-weight='700' fill='#000000' xml:space='preserve'>",
        Strings.toString(stake.dhtAmount.div(10**18)),
        ":",
        Strings.toString(vDHT.div(10**18)),
        "</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>",
        "<text id='Rewards---' xml:space='preserve'><tspan x='64' y='636' font-size='28' fill='#000000' xml:space='preserve'>Rewards:</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>",
        "<text id='10-DHT--' xml:space='preserve'><tspan x='64' y='674' font-size='32' font-weight='700' fill='#000000' xml:space='preserve'>",
        Strings.toString(rewards.div(10**18)),
        "DHT</tspan><tspan font-size='12' fill='#000000' xml:space='preserve'></tspan></text>"
      )
    );

    // Outside addresses
    guts = string(
      abi.encodePacked(
        guts,
        "<defs><path id='path24' d='M 109.299988 814.5 L 38 814.400024 C 38 814.400024 29.5 811.900024 29.300003 804 C 29.100006 794.799988 29.300003 443.5 29.300003 443.5'/></defs>"
        "<defs><path id='path25' d='M 494 29.299988 L 558 29.099976 C 558 29.099976 570 27.200012 570 43.5 C 570 61.700012 569.700012 406.299988 569.700012 406.299988'/></defs>",
        "<text id='---' xml:space='preserve'><textPath xlink:href='#path24' startOffset='1'><tspan font-size='12' fill='#000000' baseline-shift='2' xml:space='preserve'></tspan><tspan font-size='18' fill='#000000' baseline-shift='2' xml:space='preserve'>",
        addressToString(owner),
        "</tspan><tspan font-size='12' fill='#000000' baseline-shift='2' xml:space='preserve'></tspan></textPath></text>",
        "<text id='text2' xml:space='preserve'><textPath xlink:href='#path25' startOffset='1'><tspan font-size='12' fill='#000000' baseline-shift='2' xml:space='preserve'></tspan><tspan font-size='18.4567' fill='#000000' baseline-shift='2' xml:space='preserve'>",
        addressToString(dhtAddress),
        "</tspan><tspan font-size='12' fill='#000000' baseline-shift='2' xml:space='preserve'></tspan></textPath></text>"
      )
    );

    mainParts[1] = guts;
    mainParts[2] = "</svg>";

    return string(abi.encodePacked(mainParts[0], mainParts[1], mainParts[2]));
  }

  function addressToString(address _addr) public pure returns (string memory) {
    return toHexString(uint256(uint160(_addr)), 20);
  }

  function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
    bytes16 hexSymbols = "0123456789abcdef";
    bytes memory buffer = new bytes(2 * length + 2);
    buffer[0] = "0";
    buffer[1] = "x";
    for (uint256 i = 2 * length + 1; i > 1; --i) {
      buffer[i] = hexSymbols[value & 0xf];
      value >>= 4;
    }
    require(value == 0, "Strings: hex length insufficient");
    return string(buffer);
  }

  function substring(
    string memory str,
    uint256 startIndex,
    uint256 endIndex
  ) public pure returns (string memory) {
    bytes memory strBytes = bytes(str);
    bytes memory result = new bytes(endIndex - startIndex);
    for (uint256 i = startIndex; i < endIndex; i++) {
      result[i - startIndex] = strBytes[i];
    }
    return string(result);
  }
}

//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

import "@openzeppelin/contracts/math/SafeMath.sol";
import "./interfaces/IDhedgeStakingV2.sol";

pragma solidity 0.7.6;
pragma abicoder v2;

/**
 * @title A library for Calculating DHT rewards for a stake.
 */
contract DhedgeStakingV2RewardsCalculator {
  using SafeMath for uint256;

  uint256 public constant UNIT = 10**18;
  uint256 public constant POOL_TOKEN_UNIT = 10**18;

  /// @notice Calculates how many DHT a staker should recieve
  /// @dev This should take into account:
  /// @param vDHTAmount the amount of dht staked
  /// @param poolTokensStaked the number of pool tokens staked
  /// @param tokenPriceStart the price of the dhedge pool when the stake started
  /// @param tokenPriceFinish the price of the dhedge pool when the stake finished (unstaked)
  /// @param stakeStartTime when the DHPT stake started
  /// @param stakeFinishTime when the DHPT stake finished
  /// @param stakeEmissionsRate the emissions rate when the stake was created
  /// @param rewardParams current rewards configuration
  function calculateDhtRewardAmount(
    uint256 vDHTAmount,
    uint256 poolTokensStaked,
    uint256 tokenPriceStart,
    uint256 tokenPriceFinish,
    uint256 stakeStartTime,
    uint256 stakeFinishTime,
    uint256 stakeEmissionsRate,
    IDhedgeStakingV2Storage.RewardParams memory rewardParams
  ) public pure returns (uint256) {
    uint256 performanceFactor = calculatePerformanceFactor(
      tokenPriceStart,
      tokenPriceFinish,
      rewardParams.maxPerformanceBoostNumerator,
      rewardParams.maxPerformanceBoostDenominator
    );
    uint256 stakeDurationFactor = calculateStakeDurationFactor(
      stakeStartTime,
      stakeFinishTime,
      rewardParams.maxDurationBoostSeconds
    );

    uint256 totalValueStaked = tokenPriceStart.mul(poolTokensStaked).div(POOL_TOKEN_UNIT);
    uint256 maxVDHTAllowed = calculateMaxVDHTAllowed(vDHTAmount, totalValueStaked, rewardParams.stakingRatio);
    return
      (maxVDHTAllowed)
      // Both performanceFactor and stakeDurationFactor are factors and need to be divised by the unit
        .mul(performanceFactor)
        .mul(stakeDurationFactor)
        .mul(stakeEmissionsRate)
        .div(rewardParams.emissionsRateDenominator)
        .div(UNIT)
        .div(UNIT);
  }

  /// @notice Calculates the max vDHT a staker can receive based on the amount of DHPT value staked
  /// @dev The amount of vDHT is capped by the STAKING_RATIO
  /// @param vDHTAmount the amount of dht staked
  /// @param totalValue the number of pool tokens staked
  /// @param stakingRatio the price of the dhedge pool when the stake started
  function calculateMaxVDHTAllowed(
    uint256 vDHTAmount,
    uint256 totalValue,
    uint256 stakingRatio
  ) public pure returns (uint256) {
    uint256 maxVDHTForValue = totalValue.mul(stakingRatio);
    return vDHTAmount > maxVDHTForValue ? maxVDHTForValue : vDHTAmount;
  }

  /// @notice Calculates the performance factor
  /// @dev The real factor is between 0 and 1 and calculated relative to MAX_PERFORMANCE_BOOST_PERCENT
  /// @param tokenPriceStart the price of the dhedge pool when the stake started
  /// @param tokenPriceFinish the price of the dhedge pool when the stake finished (unstaked)
  /// @param maxPerformanceBoostNumerator The change in tokenPrice to acheive the maximum factor denominated by maxPerformanceBoostDenominator
  /// @param maxPerformanceBoostDenominator the denominator of factor
  function calculatePerformanceFactor(
    uint256 tokenPriceStart,
    uint256 tokenPriceFinish,
    uint256 maxPerformanceBoostNumerator,
    uint256 maxPerformanceBoostDenominator
  ) public pure returns (uint256) {
    if (tokenPriceFinish <= tokenPriceStart) {
      return 0;
    }

    uint256 profit = (tokenPriceFinish).sub(tokenPriceStart);

    uint256 diff = maxPerformanceBoostDenominator.mul(UNIT).mul(profit).div(
      maxPerformanceBoostNumerator.mul(tokenPriceStart)
    );
    if (diff > UNIT) {
      return UNIT;
    } else {
      return diff;
    }
  }

  /// @notice Calculates the DHPT stake duration factor
  /// @dev The real factor is between 0 and 1 and calculated relative to MAX_DURATION_BOOST_SECONDS
  /// @param stakeStartTime when the DHPT stake started
  /// @param stakeFinishTime when the DHPT stake finished
  /// @param maxDurationBoostSeconds The amount of time to stake to acheive the maximum factor
  function calculateStakeDurationFactor(
    uint256 stakeStartTime,
    uint256 stakeFinishTime,
    uint256 maxDurationBoostSeconds
  ) public pure returns (uint256) {
    if (stakeFinishTime <= stakeStartTime) {
      return 0;
    }
    uint256 stakeDuration = stakeFinishTime.sub(stakeStartTime);
    if (stakeDuration >= maxDurationBoostSeconds) {
      return UNIT;
    } else {
      return stakeDuration.mul(UNIT).div(maxDurationBoostSeconds);
    }
  }
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma abicoder v2;

import "./interfaces/IDhedgeStakingV2.sol";
import "./interfaces/IDhedgeStakingV2NFTJson.sol";

import "@openzeppelin/contracts/math/SafeMath.sol";
import "@openzeppelin/contracts-upgradeable/utils/CountersUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";

contract DhedgeStakingV2Storage is IDhedgeStakingV2Storage, OwnableUpgradeable {
  using CountersUpgradeable for CountersUpgradeable.Counter;
  using SafeMath for uint256;

  event OwnerOperation(string operation);

  // Address of DHT contract
  address public override dhtAddress;

  // Total amount of pools stored in poolConfiguration
  uint256 public override numberOfPoolsConfigured;
  // Allows the lookup of address by index
  mapping(uint256 => address) public override poolConfiguredByIndex;
  // Pool Configuration by pool address
  mapping(address => IDhedgeStakingV2Storage.PoolConfiguration) internal poolConfiguration;

  // tokenId -> Stake
  mapping(uint256 => IDhedgeStakingV2Storage.Stake) public stakes;

  CountersUpgradeable.Counter internal _tokenIdCounter;

  // The amount of value of dhedge pool tokens that have been staked so far
  uint256 public totalStakingValue;
  // The amount of dhtRewarded so far
  uint256 public dhtRewarded;

  // Failsafe to ensure we don't eat into staked dht when distributing rewards
  uint256 public dhtStaked;
  uint256 public aggregateStakeStartTime;

  /// OWNER EDITABLE

  // Max amount of dht emissions
  uint256 public dhtCap;
  // How long before reaching maximum vDHT
  uint256 public maxVDurationTimeSeconds;
  // How long rewards for a staked take to fully stream to a user
  uint256 public rewardStreamingTime;
  // Parameters for calculating rewards
  RewardParams public rewardParams;
  // The contract that contains the code for generating the tokenURI
  IDhedgeStakingV2NFTJson public tokenUriGenerator;

  uint256[50] private __gap;

  /// @notice Allows the owner to adjust the maxVDurationTimeSeconds
  /// @param newMaxVDurationTimeSeconds time to reach max VHDT for a staker
  function setMaxVDurationTimeSeconds(uint256 newMaxVDurationTimeSeconds) external override onlyOwner {
    require(newMaxVDurationTimeSeconds > 30 days, "Must be more than 30 days");
    require(newMaxVDurationTimeSeconds < 730 days, "Must be less than 2 years");
    maxVDurationTimeSeconds = newMaxVDurationTimeSeconds;
    emit OwnerOperation("setMaxVDurationTimeSeconds");
  }

  /// @notice Allows the owner to adjust the setStakeDurationDelaySeconds
  /// @param newStakeDurationDelaySeconds delay before a staker starts to receive rewards
  function setStakeDurationDelaySeconds(uint256 newStakeDurationDelaySeconds) external override onlyOwner {
    require(newStakeDurationDelaySeconds < 90 days, "Must be less than 90 days");
    rewardParams.stakeDurationDelaySeconds = newStakeDurationDelaySeconds;
    emit OwnerOperation("setStakeDurationDelaySeconds");
  }

  /// @notice Allows the owner to adjust the maxDurationBoostSeconds
  /// @param newMaxDurationBoostSeconds time to reach maximum stake duration boost
  function setMaxDurationBoostSeconds(uint256 newMaxDurationBoostSeconds) external override onlyOwner {
    require(newMaxDurationBoostSeconds > 30 days, "Must be more than 30 days");
    require(newMaxDurationBoostSeconds < 730 days, "Must be less than 2 years");
    rewardParams.maxDurationBoostSeconds = newMaxDurationBoostSeconds;
    emit OwnerOperation("setMaxDurationBoostSeconds");
  }

  /// @notice Allows the owner to adjust the maxPerformanceBoostNumerator
  /// @param newMaxPerformanceBoostNumerator the performance increase to reach max boost
  function setMaxPerformanceBoostNumerator(uint256 newMaxPerformanceBoostNumerator) external override onlyOwner {
    require(newMaxPerformanceBoostNumerator > 100, "Must be more than 10%");
    require(newMaxPerformanceBoostNumerator < 2000, "Must be less than 200%");
    rewardParams.maxPerformanceBoostNumerator = newMaxPerformanceBoostNumerator;
    emit OwnerOperation("setMaxPerformanceBoostNumerator");
  }

  /// @notice Allows the owner to adjust the stakingRatio
  /// @param newStakingRatio the amount of dht that can be staked per dollar of DHPT
  function setStakingRatio(uint256 newStakingRatio) external override onlyOwner {
    require(newStakingRatio > 0, "Must be greater than 0");
    require(newStakingRatio < 20, "Must be less than 20");
    rewardParams.stakingRatio = newStakingRatio;
    emit OwnerOperation("setStakingRatio");
  }

  /// @notice Allows the owner to adjust the emissionsRate
  /// @param newEmissionsRate the current emissions rate
  function setEmissionsRate(uint256 newEmissionsRate) external override onlyOwner {
    require(newEmissionsRate <= 3000, "Must be lower than 300%");
    rewardParams.emissionsRate = newEmissionsRate;
    emit OwnerOperation("setEmissionsRate");
  }

  /// @notice Allows the owner to adjust the dhtCap
  /// @dev to disable new staking set to 0
  /// @param newDHTCap max amount of aggregate value of pool tokens that can be staked
  function setDHTCap(uint256 newDHTCap) external override onlyOwner {
    require(newDHTCap <= 7_000_000 * 10**18, "Must be lower than 7 million dht");
    dhtCap = newDHTCap;
    emit OwnerOperation("setDHTCap");
  }

  /// @notice Allows the owner to adjust the rewardStreamingTime
  /// @param newRewardStreamingTime max amount of aggregate value of pool tokens that can be staked
  function setRewardStreamingTime(uint256 newRewardStreamingTime) external override onlyOwner {
    require(newRewardStreamingTime > 0, "Must be greater than 0");
    require(newRewardStreamingTime < 30 days, "Must be less than 30 days");
    rewardStreamingTime = newRewardStreamingTime;
    emit OwnerOperation("setRewardStreamingTime");
  }

  /// @notice Allows configuring a pool for staking
  /// @dev can get all pools by for(n of numberOfPoolsConfigured) poolConfiguration[allPoolsConfigured[n]].stakeCap != 0
  /// @dev to disable a pool set cap == 0
  /// @param pool the dhedge pool address
  /// @param cap The max amount of value in pooltokens that can be staked for this pool
  function configurePool(address pool, uint256 cap) external override onlyOwner {
    require(pool != address(0), "Pool cannot be address(0)");
    IDhedgeStakingV2Storage.PoolConfiguration storage pc = poolConfiguration[pool];

    if (!pc.configured) {
      pc.configured = true;
      poolConfiguredByIndex[numberOfPoolsConfigured] = pool;
      numberOfPoolsConfigured = numberOfPoolsConfigured.add(1);
    }

    pc.stakeCap = cap;
    emit OwnerOperation("configurePool");
  }

  /// @notice Allows the owner to set the tokenUriGenerator contract
  /// @param newTokenUriGenerator the address of the deployed tokenUriGenerator
  function setTokenUriGenerator(IDhedgeStakingV2NFTJson newTokenUriGenerator) external override onlyOwner {
    require(address(newTokenUriGenerator) != address(0), "Cannot be address(0)");
    tokenUriGenerator = newTokenUriGenerator;
    emit OwnerOperation("setTokenUriGenerator");
  }
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma experimental ABIEncoderV2;

import "./IDhedgeStakingV2Storage.sol";

interface IDhedgeStakingV2 {
  /// @notice Create a new stake with DHT
  /// @dev After creating dht stake user can stake Pool Tokens. User needs to approve this contract for dht first.
  /// @param dhtAmount the amount of dht being staked
  function newStake(uint256 dhtAmount) external returns (uint256 tokenId);

  /// @notice Allows the user to add addtional amount of DHT to an existing stake
  /// @dev After creating dht stake user can stake Pool Tokens. User needs to approve this contract for dht first.
  /// @param tokenId The erc721 id of the existing stake
  /// @param dhtAmount the amount of additional dht to be staked
  function addDhtToStake(uint256 tokenId, uint256 dhtAmount) external;

  /// @notice Allows the user to unstake all or some of their dht from a given stake
  /// @dev if the user calls this before calling unstakePoolTokens they may miss out on rewards
  /// @param tokenId The erc721 id of the existing stake
  /// @param dhtAmount the amount of dht they want to unstaked
  function unstakeDHT(uint256 tokenId, uint256 dhtAmount) external;

  /// @notice Allows the user to stake dhedge pool tokens with an existing DHT Stake
  /// @dev After creating dht stake user can stake Pool Tokens. User needs to approve this contract for dhedgePoolAddress first.
  /// @param tokenId The erc721 id of the existing stake
  /// @param dhedgePoolAddress the address of the pool being staked
  /// @param dhedgePoolAmount the amount of pool tokens being staked
  function stakePoolTokens(
    uint256 tokenId,
    address dhedgePoolAddress,
    uint256 dhedgePoolAmount
  ) external;

  /// @notice Allows the user to unstake their dhedge pool tokens, when called will be allocated rewards at this point.
  /// @dev Once the user unstakes their pooltokens the rewards to be recieved are calculated and assigned to the user. This stake is retired.
  /// @dev DHT is automatically zapped to a new stake that maintains it's existing vDHT. A user can then stake new pool tokens against these dht, or unstake the DHT.
  /// @param tokenId The erc721 id of the existing stake
  /// @return newTokenId the tokenId where the dht were zapped to.
  function unstakePoolTokens(uint256 tokenId) external returns (uint256 newTokenId);

  /// @notice Allows the user to claim their unlocked rewards for a given stake. The rewards are unlocked over rewardStreamingTime
  /// @dev If rewardStreamingTime == 7 days on day 7 they will be able to claim 100% of their rewards. On day 1 they will only be able to claim 1/7th.
  /// @param tokenId The erc721 id of the existing stake
  function claim(uint256 tokenId) external;

  /// @notice Returns the amount of rewards unlocked so far for a given stake
  /// @dev If rewardStreamingTime == 7 days on day 7 they will be able to claim 100% of their rewards. On day 1 they will only be able to claim 1/7th.
  /// @param tokenId The erc721 id of the existing stake
  function canClaimAmount(uint256 tokenId) external returns (uint256);

  /// @notice The aggregate DHT balance of the wallet
  /// @param staker The the wallet
  function dhtBalanceOf(address staker) external view returns (uint256 dht);

  /// @notice The aggregate vDHT balance of the wallet
  /// @dev Can be used for voting
  /// @param staker The the wallet
  /// @return vDHT the current vDHT for the given wallet
  function vDHTBalanceOf(address staker) external view returns (uint256 vDHT);

  /// @notice Returns the current vDHT of a stake
  /// @dev this changes every block based on the time passed since staking
  /// @param tokenId the id of the stake
  /// @return vDHT the current vDHT for the given stake
  function vDHTBalanceOfStake(uint256 tokenId) external view returns (uint256 vDHT);

  /// @notice Allows getting configuration of a pool
  /// @param poolAddress the dhedge pool address to get the configuration for
  /// @return poolConfiguration the configuration for the given pool
  function getPoolConfiguration(address poolAddress)
    external
    view
    returns (IDhedgeStakingV2Storage.PoolConfiguration memory);

  /// @notice Allows getting stake info
  /// @param tokenId the erc721 id of the stake
  /// @return stake the stake struct for the given tokenID
  function getStake(uint256 tokenId) external view returns (IDhedgeStakingV2Storage.Stake memory);

  /// @notice The rewards a staker would receive if they unstaked now
  /// @param tokenId the id of the stake
  /// @return rewardsDHT the current aggregate DHT for the address
  function currentRewardsForStake(uint256 tokenId) external view returns (uint256 rewardsDHT);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMathUpgradeable {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        uint256 c = a + b;
        if (c < a) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b > a) return (false, 0);
        return (true, a - b);
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) return (true, 0);
        uint256 c = a * b;
        if (c / a != b) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a / b);
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a % b);
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) return 0;
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: division by zero");
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: modulo by zero");
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        return a - b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryDiv}.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a % b;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "../math/SafeMathUpgradeable.sol";

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented or decremented by one. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 * Since it is not possible to overflow a 256 bit integer with increments of one, `increment` can skip the {SafeMath}
 * overflow check, thereby saving gas. This does assume however correct usage, in that the underlying `_value` is never
 * directly accessed.
 */
library CountersUpgradeable {
    using SafeMathUpgradeable for uint256;

    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        // The {SafeMath} overflow check can be skipped here, see the comment at the top
        counter._value += 1;
    }

    function decrement(Counter storage counter) internal {
        counter._value = counter._value.sub(1);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "./IERC20Upgradeable.sol";
import "../../math/SafeMathUpgradeable.sol";
import "../../utils/AddressUpgradeable.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20Upgradeable {
    using SafeMathUpgradeable for uint256;
    using AddressUpgradeable for address;

    function safeTransfer(IERC20Upgradeable token, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(IERC20Upgradeable token, address from, address to, uint256 value) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(IERC20Upgradeable token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        // solhint-disable-next-line max-line-length
        require((value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(IERC20Upgradeable token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender).add(value);
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(IERC20Upgradeable token, address spender, uint256 value) internal {
        uint256 newAllowance = token.allowance(address(this), spender).sub(value, "SafeERC20: decreased allowance below zero");
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20Upgradeable token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address.functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) { // Return data is optional
            // solhint-disable-next-line max-line-length
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20Upgradeable {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "../../utils/ContextUpgradeable.sol";
import "./IERC721Upgradeable.sol";
import "./IERC721MetadataUpgradeable.sol";
import "./IERC721EnumerableUpgradeable.sol";
import "./IERC721ReceiverUpgradeable.sol";
import "../../introspection/ERC165Upgradeable.sol";
import "../../math/SafeMathUpgradeable.sol";
import "../../utils/AddressUpgradeable.sol";
import "../../utils/EnumerableSetUpgradeable.sol";
import "../../utils/EnumerableMapUpgradeable.sol";
import "../../utils/StringsUpgradeable.sol";
import "../../proxy/Initializable.sol";

/**
 * @title ERC721 Non-Fungible Token Standard basic implementation
 * @dev see https://eips.ethereum.org/EIPS/eip-721
 */
contract ERC721Upgradeable is Initializable, ContextUpgradeable, ERC165Upgradeable, IERC721Upgradeable, IERC721MetadataUpgradeable, IERC721EnumerableUpgradeable {
    using SafeMathUpgradeable for uint256;
    using AddressUpgradeable for address;
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.UintSet;
    using EnumerableMapUpgradeable for EnumerableMapUpgradeable.UintToAddressMap;
    using StringsUpgradeable for uint256;

    // Equals to `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`
    // which can be also obtained as `IERC721Receiver(0).onERC721Received.selector`
    bytes4 private constant _ERC721_RECEIVED = 0x150b7a02;

    // Mapping from holder address to their (enumerable) set of owned tokens
    mapping (address => EnumerableSetUpgradeable.UintSet) private _holderTokens;

    // Enumerable mapping from token ids to their owners
    EnumerableMapUpgradeable.UintToAddressMap private _tokenOwners;

    // Mapping from token ID to approved address
    mapping (uint256 => address) private _tokenApprovals;

    // Mapping from owner to operator approvals
    mapping (address => mapping (address => bool)) private _operatorApprovals;

    // Token name
    string private _name;

    // Token symbol
    string private _symbol;

    // Optional mapping for token URIs
    mapping (uint256 => string) private _tokenURIs;

    // Base URI
    string private _baseURI;

    /*
     *     bytes4(keccak256('balanceOf(address)')) == 0x70a08231
     *     bytes4(keccak256('ownerOf(uint256)')) == 0x6352211e
     *     bytes4(keccak256('approve(address,uint256)')) == 0x095ea7b3
     *     bytes4(keccak256('getApproved(uint256)')) == 0x081812fc
     *     bytes4(keccak256('setApprovalForAll(address,bool)')) == 0xa22cb465
     *     bytes4(keccak256('isApprovedForAll(address,address)')) == 0xe985e9c5
     *     bytes4(keccak256('transferFrom(address,address,uint256)')) == 0x23b872dd
     *     bytes4(keccak256('safeTransferFrom(address,address,uint256)')) == 0x42842e0e
     *     bytes4(keccak256('safeTransferFrom(address,address,uint256,bytes)')) == 0xb88d4fde
     *
     *     => 0x70a08231 ^ 0x6352211e ^ 0x095ea7b3 ^ 0x081812fc ^
     *        0xa22cb465 ^ 0xe985e9c5 ^ 0x23b872dd ^ 0x42842e0e ^ 0xb88d4fde == 0x80ac58cd
     */
    bytes4 private constant _INTERFACE_ID_ERC721 = 0x80ac58cd;

    /*
     *     bytes4(keccak256('name()')) == 0x06fdde03
     *     bytes4(keccak256('symbol()')) == 0x95d89b41
     *     bytes4(keccak256('tokenURI(uint256)')) == 0xc87b56dd
     *
     *     => 0x06fdde03 ^ 0x95d89b41 ^ 0xc87b56dd == 0x5b5e139f
     */
    bytes4 private constant _INTERFACE_ID_ERC721_METADATA = 0x5b5e139f;

    /*
     *     bytes4(keccak256('totalSupply()')) == 0x18160ddd
     *     bytes4(keccak256('tokenOfOwnerByIndex(address,uint256)')) == 0x2f745c59
     *     bytes4(keccak256('tokenByIndex(uint256)')) == 0x4f6ccce7
     *
     *     => 0x18160ddd ^ 0x2f745c59 ^ 0x4f6ccce7 == 0x780e9d63
     */
    bytes4 private constant _INTERFACE_ID_ERC721_ENUMERABLE = 0x780e9d63;

    /**
     * @dev Initializes the contract by setting a `name` and a `symbol` to the token collection.
     */
    function __ERC721_init(string memory name_, string memory symbol_) internal initializer {
        __Context_init_unchained();
        __ERC165_init_unchained();
        __ERC721_init_unchained(name_, symbol_);
    }

    function __ERC721_init_unchained(string memory name_, string memory symbol_) internal initializer {
        _name = name_;
        _symbol = symbol_;

        // register the supported interfaces to conform to ERC721 via ERC165
        _registerInterface(_INTERFACE_ID_ERC721);
        _registerInterface(_INTERFACE_ID_ERC721_METADATA);
        _registerInterface(_INTERFACE_ID_ERC721_ENUMERABLE);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(address owner) public view virtual override returns (uint256) {
        require(owner != address(0), "ERC721: balance query for the zero address");
        return _holderTokens[owner].length();
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(uint256 tokenId) public view virtual override returns (address) {
        return _tokenOwners.get(tokenId, "ERC721: owner query for nonexistent token");
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _symbol;
    }

    /**
     * @dev See {IERC721Metadata-tokenURI}.
     */
    function tokenURI(uint256 tokenId) public view virtual override returns (string memory) {
        require(_exists(tokenId), "ERC721Metadata: URI query for nonexistent token");

        string memory _tokenURI = _tokenURIs[tokenId];
        string memory base = baseURI();

        // If there is no base URI, return the token URI.
        if (bytes(base).length == 0) {
            return _tokenURI;
        }
        // If both are set, concatenate the baseURI and tokenURI (via abi.encodePacked).
        if (bytes(_tokenURI).length > 0) {
            return string(abi.encodePacked(base, _tokenURI));
        }
        // If there is a baseURI but no tokenURI, concatenate the tokenID to the baseURI.
        return string(abi.encodePacked(base, tokenId.toString()));
    }

    /**
    * @dev Returns the base URI set via {_setBaseURI}. This will be
    * automatically added as a prefix in {tokenURI} to each token's URI, or
    * to the token ID if no specific URI is set for that token ID.
    */
    function baseURI() public view virtual returns (string memory) {
        return _baseURI;
    }

    /**
     * @dev See {IERC721Enumerable-tokenOfOwnerByIndex}.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) public view virtual override returns (uint256) {
        return _holderTokens[owner].at(index);
    }

    /**
     * @dev See {IERC721Enumerable-totalSupply}.
     */
    function totalSupply() public view virtual override returns (uint256) {
        // _tokenOwners are indexed by tokenIds, so .length() returns the number of tokenIds
        return _tokenOwners.length();
    }

    /**
     * @dev See {IERC721Enumerable-tokenByIndex}.
     */
    function tokenByIndex(uint256 index) public view virtual override returns (uint256) {
        (uint256 tokenId, ) = _tokenOwners.at(index);
        return tokenId;
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ERC721Upgradeable.ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(_msgSender() == owner || ERC721Upgradeable.isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not owner nor approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(uint256 tokenId) public view virtual override returns (address) {
        require(_exists(tokenId), "ERC721: approved query for nonexistent token");

        return _tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(address operator, bool approved) public virtual override {
        require(operator != _msgSender(), "ERC721: approve to caller");

        _operatorApprovals[_msgSender()][operator] = approved;
        emit ApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(address owner, address operator) public view virtual override returns (bool) {
        return _operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(address from, address to, uint256 tokenId) public virtual override {
        //solhint-disable-next-line max-line-length
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory _data) public virtual override {
        require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC721: transfer caller is not owner nor approved");
        _safeTransfer(from, to, tokenId, _data);
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `_data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(address from, address to, uint256 tokenId, bytes memory _data) internal virtual {
        _transfer(from, to, tokenId);
        require(_checkOnERC721Received(from, to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _tokenOwners.contains(tokenId);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view virtual returns (bool) {
        require(_exists(tokenId), "ERC721: operator query for nonexistent token");
        address owner = ERC721Upgradeable.ownerOf(tokenId);
        return (spender == owner || getApproved(tokenId) == spender || ERC721Upgradeable.isApprovedForAll(owner, spender));
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     d*
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(address to, uint256 tokenId, bytes memory _data) internal virtual {
        _mint(to, tokenId);
        require(_checkOnERC721Received(address(0), to, tokenId, _data), "ERC721: transfer to non ERC721Receiver implementer");
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId);

        _holderTokens[to].add(tokenId);

        _tokenOwners.set(tokenId, to);

        emit Transfer(address(0), to, tokenId);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ERC721Upgradeable.ownerOf(tokenId); // internal owner

        _beforeTokenTransfer(owner, address(0), tokenId);

        // Clear approvals
        _approve(address(0), tokenId);

        // Clear metadata (if any)
        if (bytes(_tokenURIs[tokenId]).length != 0) {
            delete _tokenURIs[tokenId];
        }

        _holderTokens[owner].remove(tokenId);

        _tokenOwners.remove(tokenId);

        emit Transfer(owner, address(0), tokenId);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(address from, address to, uint256 tokenId) internal virtual {
        require(ERC721Upgradeable.ownerOf(tokenId) == from, "ERC721: transfer of token that is not own"); // internal owner
        require(to != address(0), "ERC721: transfer to the zero address");

        _beforeTokenTransfer(from, to, tokenId);

        // Clear approvals from the previous owner
        _approve(address(0), tokenId);

        _holderTokens[from].remove(tokenId);
        _holderTokens[to].add(tokenId);

        _tokenOwners.set(tokenId, to);

        emit Transfer(from, to, tokenId);
    }

    /**
     * @dev Sets `_tokenURI` as the tokenURI of `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _setTokenURI(uint256 tokenId, string memory _tokenURI) internal virtual {
        require(_exists(tokenId), "ERC721Metadata: URI set of nonexistent token");
        _tokenURIs[tokenId] = _tokenURI;
    }

    /**
     * @dev Internal function to set the base URI for all token IDs. It is
     * automatically added as a prefix to the value returned in {tokenURI},
     * or to the token ID if {tokenURI} is empty.
     */
    function _setBaseURI(string memory baseURI_) internal virtual {
        _baseURI = baseURI_;
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param _data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(address from, address to, uint256 tokenId, bytes memory _data)
        private returns (bool)
    {
        if (!to.isContract()) {
            return true;
        }
        bytes memory returndata = to.functionCall(abi.encodeWithSelector(
            IERC721ReceiverUpgradeable(to).onERC721Received.selector,
            _msgSender(),
            from,
            tokenId,
            _data
        ), "ERC721: transfer to non ERC721Receiver implementer");
        bytes4 retval = abi.decode(returndata, (bytes4));
        return (retval == _ERC721_RECEIVED);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits an {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _tokenApprovals[tokenId] = to;
        emit Approval(ERC721Upgradeable.ownerOf(tokenId), to, tokenId); // internal owner
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting
     * and burning.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s `tokenId` will be
     * transferred to `to`.
     * - When `from` is zero, `tokenId` will be minted for `to`.
     * - When `to` is zero, ``from``'s `tokenId` will be burned.
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(address from, address to, uint256 tokenId) internal virtual { }
    uint256[41] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "./ContextUpgradeable.sol";
import "../proxy/Initializable.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract PausableUpgradeable is Initializable, ContextUpgradeable {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    function __Pausable_init() internal initializer {
        __Context_init_unchained();
        __Pausable_init_unchained();
    }

    function __Pausable_init_unchained() internal initializer {
        _paused = false;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        require(!paused(), "Pausable: paused");
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        require(paused(), "Pausable: not paused");
        _;
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev String operations.
 */
library Strings {
    /**
     * @dev Converts a `uint256` to its ASCII `string` representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        uint256 index = digits - 1;
        temp = value;
        while (temp != 0) {
            buffer[index--] = bytes1(uint8(48 + temp % 10));
            temp /= 10;
        }
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;
import "../proxy/Initializable.sol";

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuardUpgradeable is Initializable {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    function __ReentrancyGuard_init() internal initializer {
        __ReentrancyGuard_init_unchained();
    }

    function __ReentrancyGuard_init_unchained() internal initializer {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and make it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Wrappers over Solidity's arithmetic operations with added overflow
 * checks.
 *
 * Arithmetic operations in Solidity wrap on overflow. This can easily result
 * in bugs, because programmers usually assume that an overflow raises an
 * error, which is the standard behavior in high level programming languages.
 * `SafeMath` restores this intuition by reverting the transaction when an
 * operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 */
library SafeMath {
    /**
     * @dev Returns the addition of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryAdd(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        uint256 c = a + b;
        if (c < a) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the substraction of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function trySub(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b > a) return (false, 0);
        return (true, a - b);
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, with an overflow flag.
     *
     * _Available since v3.4._
     */
    function tryMul(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the
        // benefit is lost if 'b' is also tested.
        // See: https://github.com/OpenZeppelin/openzeppelin-contracts/pull/522
        if (a == 0) return (true, 0);
        uint256 c = a * b;
        if (c / a != b) return (false, 0);
        return (true, c);
    }

    /**
     * @dev Returns the division of two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryDiv(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a / b);
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers, with a division by zero flag.
     *
     * _Available since v3.4._
     */
    function tryMod(uint256 a, uint256 b) internal pure returns (bool, uint256) {
        if (b == 0) return (false, 0);
        return (true, a % b);
    }

    /**
     * @dev Returns the addition of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `+` operator.
     *
     * Requirements:
     *
     * - Addition cannot overflow.
     */
    function add(uint256 a, uint256 b) internal pure returns (uint256) {
        uint256 c = a + b;
        require(c >= a, "SafeMath: addition overflow");
        return c;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting on
     * overflow (when the result is negative).
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b <= a, "SafeMath: subtraction overflow");
        return a - b;
    }

    /**
     * @dev Returns the multiplication of two unsigned integers, reverting on
     * overflow.
     *
     * Counterpart to Solidity's `*` operator.
     *
     * Requirements:
     *
     * - Multiplication cannot overflow.
     */
    function mul(uint256 a, uint256 b) internal pure returns (uint256) {
        if (a == 0) return 0;
        uint256 c = a * b;
        require(c / a == b, "SafeMath: multiplication overflow");
        return c;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting on
     * division by zero. The result is rounded towards zero.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: division by zero");
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting when dividing by zero.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b) internal pure returns (uint256) {
        require(b > 0, "SafeMath: modulo by zero");
        return a % b;
    }

    /**
     * @dev Returns the subtraction of two unsigned integers, reverting with custom message on
     * overflow (when the result is negative).
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {trySub}.
     *
     * Counterpart to Solidity's `-` operator.
     *
     * Requirements:
     *
     * - Subtraction cannot overflow.
     */
    function sub(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b <= a, errorMessage);
        return a - b;
    }

    /**
     * @dev Returns the integer division of two unsigned integers, reverting with custom message on
     * division by zero. The result is rounded towards zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryDiv}.
     *
     * Counterpart to Solidity's `/` operator. Note: this function uses a
     * `revert` opcode (which leaves remaining gas untouched) while Solidity
     * uses an invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function div(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a / b;
    }

    /**
     * @dev Returns the remainder of dividing two unsigned integers. (unsigned integer modulo),
     * reverting with custom message when dividing by zero.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryMod}.
     *
     * Counterpart to Solidity's `%` operator. This function uses a `revert`
     * opcode (which leaves remaining gas untouched) while Solidity uses an
     * invalid opcode to revert (consuming all remaining gas).
     *
     * Requirements:
     *
     * - The divisor cannot be zero.
     */
    function mod(uint256 a, uint256 b, string memory errorMessage) internal pure returns (uint256) {
        require(b > 0, errorMessage);
        return a % b;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "../utils/Context.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor () {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma experimental ABIEncoderV2;

import "./IDhedgeStakingV2NFTJson.sol";

interface IDhedgeStakingV2Storage {
  struct Stake {
    uint256 dhtAmount;
    uint256 dhtStakeStartTime;
    address dhedgePoolAddress;
    uint256 dhedgePoolAmount;
    uint256 dhedgePoolStakeStartTime;
    uint256 stakeStartTokenPrice;
    bool unstaked;
    uint256 unstakeTime;
    uint256 reward;
    uint256 claimedReward;
    uint256 rewardParamsEmissionsRate;
    uint256 stakeFinishTokenPrice;
    uint256 vdhtAccruedAtUnstake;
    uint256 dhedgePoolRemainingExitCooldownAtStakeTime;
  }

  struct PoolConfiguration {
    bool configured;
    uint256 stakeCap;
    uint256 stakedSoFar;
  }

  struct RewardParams {
    uint256 stakeDurationDelaySeconds;
    uint256 maxDurationBoostSeconds;
    uint256 maxPerformanceBoostNumerator;
    uint256 maxPerformanceBoostDenominator;
    uint256 stakingRatio;
    uint256 emissionsRate;
    uint256 emissionsRateDenominator;
  }

  /// Only Owner

  /// @notice Allows the owner to allow staking of a pool by setting a cap > 0
  /// @dev can also be used to restrict staking of a pool by setting cap back to 0
  function configurePool(address pool, uint256 cap) external;

  /// @notice Allows the owner to modify the dhtCap which controls the max staking value
  /// @dev can also be used to restrict staking cap back to 0 or rewardedDHT
  function setDHTCap(uint256 newDHTCap) external;

  /// @notice Allows the owner to adjust the maxVDurationTimeSeconds
  /// @param newMaxVDurationTimeSeconds time to reach max VHDT for a staker
  function setMaxVDurationTimeSeconds(uint256 newMaxVDurationTimeSeconds) external;

  /// @notice Allows the owner to adjust the setStakeDurationDelaySeconds
  /// @param newStakeDurationDelaySeconds delay before a staker starts to receive rewards
  function setStakeDurationDelaySeconds(uint256 newStakeDurationDelaySeconds) external;

  /// @notice Allows the owner to adjust the maxDurationBoostSeconds
  /// @param newMaxDurationBoostSeconds time to reach maximum stake duration boost
  function setMaxDurationBoostSeconds(uint256 newMaxDurationBoostSeconds) external;

  /// @notice Allows the owner to adjust the maxPerformanceBoostNumerator
  /// @param newMaxPerformanceBoostNumerator the performance increase to reach max boost
  function setMaxPerformanceBoostNumerator(uint256 newMaxPerformanceBoostNumerator) external;

  /// @notice Allows the owner to adjust the stakingRatio
  /// @param newStakingRatio the amount of dht that can be staked per dollar of DHPT
  function setStakingRatio(uint256 newStakingRatio) external;

  /// @notice Allows the owner to adjust the emissionsRate
  /// @param newEmissionsRate currently 1 not used
  function setEmissionsRate(uint256 newEmissionsRate) external;

  /// @notice Allows the owner to adjust the rewardStreamingTime
  /// @param newRewardStreamingTime max amount of aggregate value of pool tokens that can be staked
  function setRewardStreamingTime(uint256 newRewardStreamingTime) external;

  /// VIEW

  /// @notice The contract address for DHT
  function dhtAddress() external view returns (address);

  /// @notice The total number of pools configured for staking
  /// @dev can be used with poolConfiguredByIndex and poolConfiguration to look up all existing pool configs
  function numberOfPoolsConfigured() external returns (uint256 numberOfPools);

  /// @notice Returns the poolAddress stored at the index
  /// @dev can be used with numberOfPoolsConfigured and poolConfiguration to get all information about configured pools
  /// @param index the index to look up
  /// @return poolAddress the address at the index
  function poolConfiguredByIndex(uint256 index) external returns (address poolAddress);

  /// @notice Allows the owner to set the tokenUriGenerator contract
  /// @param newTokenUriGenerator the address of the deployed tokenUriGenerator
  function setTokenUriGenerator(IDhedgeStakingV2NFTJson newTokenUriGenerator) external;
}

//
//        __  __    __  ________  _______    ______   ________
//       /  |/  |  /  |/        |/       \  /      \ /        |
//   ____$$ |$$ |  $$ |$$$$$$$$/ $$$$$$$  |/$$$$$$  |$$$$$$$$/
//  /    $$ |$$ |__$$ |$$ |__    $$ |  $$ |$$ | _$$/ $$ |__
// /$$$$$$$ |$$    $$ |$$    |   $$ |  $$ |$$ |/    |$$    |
// $$ |  $$ |$$$$$$$$ |$$$$$/    $$ |  $$ |$$ |$$$$ |$$$$$/
// $$ \__$$ |$$ |  $$ |$$ |_____ $$ |__$$ |$$ \__$$ |$$ |_____
// $$    $$ |$$ |  $$ |$$       |$$    $$/ $$    $$/ $$       |
//  $$$$$$$/ $$/   $$/ $$$$$$$$/ $$$$$$$/   $$$$$$/  $$$$$$$$/
//
// dHEDGE DAO - https://dhedge.org
//
// Copyright (c) 2021 dHEDGE DAO
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//
// SPDX-License-Identifier: MIT

pragma solidity 0.7.6;
pragma abicoder v2;

import "../Base64.sol";
import "./IDhedgeStakingV2Storage.sol";

interface IDhedgeStakingV2NFTJson {
  function tokenJson(
    uint256 tokenId,
    IDhedgeStakingV2Storage.Stake memory stake,
    uint256 vDHT,
    uint256 rewards,
    string memory poolSymbol,
    uint256 currentTokenPrice,
    address dhtAddress,
    address owner
  ) external view returns (string memory);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/Initializable.sol";
/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal initializer {
        __Context_init_unchained();
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal initializer {
        address msgSender = _msgSender();
        _owner = msgSender;
        emit OwnershipTransferred(address(0), msgSender);
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        emit OwnershipTransferred(_owner, address(0));
        _owner = address(0);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT

// solhint-disable-next-line compiler-version
pragma solidity >=0.4.24 <0.8.0;

import "../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since a proxied contract can't have a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {UpgradeableProxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 */
abstract contract Initializable {

    /**
     * @dev Indicates that the contract has been initialized.
     */
    bool private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Modifier to protect an initializer function from being invoked twice.
     */
    modifier initializer() {
        require(_initializing || _isConstructor() || !_initialized, "Initializable: contract is already initialized");

        bool isTopLevelCall = !_initializing;
        if (isTopLevelCall) {
            _initializing = true;
            _initialized = true;
        }

        _;

        if (isTopLevelCall) {
            _initializing = false;
        }
    }

    /// @dev Returns true if and only if the function is running in the constructor
    function _isConstructor() private view returns (bool) {
        return !AddressUpgradeable.isContract(address(this));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;
import "../proxy/Initializable.sol";

/*
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with GSN meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal initializer {
        __Context_init_unchained();
    }

    function __Context_init_unchained() internal initializer {
    }
    function _msgSender() internal view virtual returns (address payable) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes memory) {
        this; // silence state mutability warning without generating bytecode - see https://github.com/ethereum/solidity/issues/2691
        return msg.data;
    }
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize, which returns 0 for contracts in
        // construction, since the code is only stored at the end of the
        // constructor execution.

        uint256 size;
        // solhint-disable-next-line no-inline-assembly
        assembly { size := extcodesize(account) }
        return size > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        // solhint-disable-next-line avoid-low-level-calls, avoid-call-value
        (bool success, ) = recipient.call{ value: amount }("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain`call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
      return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data, string memory errorMessage) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value, string memory errorMessage) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.call{ value: value }(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data, string memory errorMessage) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returndata) = target.staticcall(data);
        return _verifyCallResult(success, returndata, errorMessage);
    }

    function _verifyCallResult(bool success, bytes memory returndata, string memory errorMessage) private pure returns(bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly

                // solhint-disable-next-line no-inline-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Library for managing an enumerable variant of Solidity's
 * https://solidity.readthedocs.io/en/latest/types.html#mapping-types[`mapping`]
 * type.
 *
 * Maps have the following properties:
 *
 * - Entries are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Entries are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableMap for EnumerableMap.UintToAddressMap;
 *
 *     // Declare a set state variable
 *     EnumerableMap.UintToAddressMap private myMap;
 * }
 * ```
 *
 * As of v3.0.0, only maps of type `uint256 -> address` (`UintToAddressMap`) are
 * supported.
 */
library EnumerableMapUpgradeable {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Map type with
    // bytes32 keys and values.
    // The Map implementation uses private functions, and user-facing
    // implementations (such as Uint256ToAddressMap) are just wrappers around
    // the underlying Map.
    // This means that we can only create new EnumerableMaps for types that fit
    // in bytes32.

    struct MapEntry {
        bytes32 _key;
        bytes32 _value;
    }

    struct Map {
        // Storage of map keys and values
        MapEntry[] _entries;

        // Position of the entry defined by a key in the `entries` array, plus 1
        // because index 0 means a key is not in the map.
        mapping (bytes32 => uint256) _indexes;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function _set(Map storage map, bytes32 key, bytes32 value) private returns (bool) {
        // We read and store the key's index to prevent multiple reads from the same storage slot
        uint256 keyIndex = map._indexes[key];

        if (keyIndex == 0) { // Equivalent to !contains(map, key)
            map._entries.push(MapEntry({ _key: key, _value: value }));
            // The entry is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            map._indexes[key] = map._entries.length;
            return true;
        } else {
            map._entries[keyIndex - 1]._value = value;
            return false;
        }
    }

    /**
     * @dev Removes a key-value pair from a map. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function _remove(Map storage map, bytes32 key) private returns (bool) {
        // We read and store the key's index to prevent multiple reads from the same storage slot
        uint256 keyIndex = map._indexes[key];

        if (keyIndex != 0) { // Equivalent to contains(map, key)
            // To delete a key-value pair from the _entries array in O(1), we swap the entry to delete with the last one
            // in the array, and then remove the last entry (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = keyIndex - 1;
            uint256 lastIndex = map._entries.length - 1;

            // When the entry to delete is the last one, the swap operation is unnecessary. However, since this occurs
            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.

            MapEntry storage lastEntry = map._entries[lastIndex];

            // Move the last entry to the index where the entry to delete is
            map._entries[toDeleteIndex] = lastEntry;
            // Update the index for the moved entry
            map._indexes[lastEntry._key] = toDeleteIndex + 1; // All indexes are 1-based

            // Delete the slot where the moved entry was stored
            map._entries.pop();

            // Delete the index for the deleted slot
            delete map._indexes[key];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function _contains(Map storage map, bytes32 key) private view returns (bool) {
        return map._indexes[key] != 0;
    }

    /**
     * @dev Returns the number of key-value pairs in the map. O(1).
     */
    function _length(Map storage map) private view returns (uint256) {
        return map._entries.length;
    }

   /**
    * @dev Returns the key-value pair stored at position `index` in the map. O(1).
    *
    * Note that there are no guarantees on the ordering of entries inside the
    * array, and it may change when more entries are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function _at(Map storage map, uint256 index) private view returns (bytes32, bytes32) {
        require(map._entries.length > index, "EnumerableMap: index out of bounds");

        MapEntry storage entry = map._entries[index];
        return (entry._key, entry._value);
    }

    /**
     * @dev Tries to returns the value associated with `key`.  O(1).
     * Does not revert if `key` is not in the map.
     */
    function _tryGet(Map storage map, bytes32 key) private view returns (bool, bytes32) {
        uint256 keyIndex = map._indexes[key];
        if (keyIndex == 0) return (false, 0); // Equivalent to contains(map, key)
        return (true, map._entries[keyIndex - 1]._value); // All indexes are 1-based
    }

    /**
     * @dev Returns the value associated with `key`.  O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function _get(Map storage map, bytes32 key) private view returns (bytes32) {
        uint256 keyIndex = map._indexes[key];
        require(keyIndex != 0, "EnumerableMap: nonexistent key"); // Equivalent to contains(map, key)
        return map._entries[keyIndex - 1]._value; // All indexes are 1-based
    }

    /**
     * @dev Same as {_get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {_tryGet}.
     */
    function _get(Map storage map, bytes32 key, string memory errorMessage) private view returns (bytes32) {
        uint256 keyIndex = map._indexes[key];
        require(keyIndex != 0, errorMessage); // Equivalent to contains(map, key)
        return map._entries[keyIndex - 1]._value; // All indexes are 1-based
    }

    // UintToAddressMap

    struct UintToAddressMap {
        Map _inner;
    }

    /**
     * @dev Adds a key-value pair to a map, or updates the value for an existing
     * key. O(1).
     *
     * Returns true if the key was added to the map, that is if it was not
     * already present.
     */
    function set(UintToAddressMap storage map, uint256 key, address value) internal returns (bool) {
        return _set(map._inner, bytes32(key), bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the key was removed from the map, that is if it was present.
     */
    function remove(UintToAddressMap storage map, uint256 key) internal returns (bool) {
        return _remove(map._inner, bytes32(key));
    }

    /**
     * @dev Returns true if the key is in the map. O(1).
     */
    function contains(UintToAddressMap storage map, uint256 key) internal view returns (bool) {
        return _contains(map._inner, bytes32(key));
    }

    /**
     * @dev Returns the number of elements in the map. O(1).
     */
    function length(UintToAddressMap storage map) internal view returns (uint256) {
        return _length(map._inner);
    }

   /**
    * @dev Returns the element stored at position `index` in the set. O(1).
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(UintToAddressMap storage map, uint256 index) internal view returns (uint256, address) {
        (bytes32 key, bytes32 value) = _at(map._inner, index);
        return (uint256(key), address(uint160(uint256(value))));
    }

    /**
     * @dev Tries to returns the value associated with `key`.  O(1).
     * Does not revert if `key` is not in the map.
     *
     * _Available since v3.4._
     */
    function tryGet(UintToAddressMap storage map, uint256 key) internal view returns (bool, address) {
        (bool success, bytes32 value) = _tryGet(map._inner, bytes32(key));
        return (success, address(uint160(uint256(value))));
    }

    /**
     * @dev Returns the value associated with `key`.  O(1).
     *
     * Requirements:
     *
     * - `key` must be in the map.
     */
    function get(UintToAddressMap storage map, uint256 key) internal view returns (address) {
        return address(uint160(uint256(_get(map._inner, bytes32(key)))));
    }

    /**
     * @dev Same as {get}, with a custom error message when `key` is not in the map.
     *
     * CAUTION: This function is deprecated because it requires allocating memory for the error
     * message unnecessarily. For custom revert reasons use {tryGet}.
     */
    function get(UintToAddressMap storage map, uint256 key, string memory errorMessage) internal view returns (address) {
        return address(uint160(uint256(_get(map._inner, bytes32(key), errorMessage))));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "./IERC165Upgradeable.sol";
import "../proxy/Initializable.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts may inherit from this and call {_registerInterface} to declare
 * their support of an interface.
 */
abstract contract ERC165Upgradeable is Initializable, IERC165Upgradeable {
    /*
     * bytes4(keccak256('supportsInterface(bytes4)')) == 0x01ffc9a7
     */
    bytes4 private constant _INTERFACE_ID_ERC165 = 0x01ffc9a7;

    /**
     * @dev Mapping of interface ids to whether or not it's supported.
     */
    mapping(bytes4 => bool) private _supportedInterfaces;

    function __ERC165_init() internal initializer {
        __ERC165_init_unchained();
    }

    function __ERC165_init_unchained() internal initializer {
        // Derived contracts need only register support for their own interfaces,
        // we register support for ERC165 itself here
        _registerInterface(_INTERFACE_ID_ERC165);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     *
     * Time complexity O(1), guaranteed to always use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return _supportedInterfaces[interfaceId];
    }

    /**
     * @dev Registers the contract as an implementer of the interface defined by
     * `interfaceId`. Support of the actual ERC165 interface is automatic and
     * registering its interface id is not required.
     *
     * See {IERC165-supportsInterface}.
     *
     * Requirements:
     *
     * - `interfaceId` cannot be the ERC165 invalid interface (`0xffffffff`).
     */
    function _registerInterface(bytes4 interfaceId) internal virtual {
        require(interfaceId != 0xffffffff, "ERC165: invalid interface id");
        _supportedInterfaces[interfaceId] = true;
    }
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 */
library EnumerableSetUpgradeable {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;

        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping (bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) { // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            // When the value to delete is the last one, the swap operation is unnecessary. However, since this occurs
            // so rarely, we still do the swap anyway to avoid the gas cost of adding an 'if' statement.

            bytes32 lastvalue = set._values[lastIndex];

            // Move the last value to the index where the value to delete is
            set._values[toDeleteIndex] = lastvalue;
            // Update the index for the moved value
            set._indexes[lastvalue] = toDeleteIndex + 1; // All indexes are 1-based

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        require(set._values.length > index, "EnumerableSet: index out of bounds");
        return set._values[index];
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }


    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

   /**
    * @dev Returns the value stored at position `index` in the set. O(1).
    *
    * Note that there are no guarantees on the ordering of values inside the
    * array, and it may change when more values are added or removed.
    *
    * Requirements:
    *
    * - `index` must be strictly less than {length}.
    */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev String operations.
 */
library StringsUpgradeable {
    /**
     * @dev Converts a `uint256` to its ASCII `string` representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        // Inspired by OraclizeAPI's implementation - MIT licence
        // https://github.com/oraclize/ethereum-api/blob/b42146b063c7d6ee1358846c198246239e9360e8/oraclizeAPI_0.4.25.sol

        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        uint256 index = digits - 1;
        temp = value;
        while (temp != 0) {
            buffer[index--] = bytes1(uint8(48 + temp % 10));
            temp /= 10;
        }
        return string(buffer);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "./IERC721Upgradeable.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional enumeration extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721EnumerableUpgradeable is IERC721Upgradeable {

    /**
     * @dev Returns the total amount of tokens stored by the contract.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns a token ID owned by `owner` at a given `index` of its token list.
     * Use along with {balanceOf} to enumerate all of ``owner``'s tokens.
     */
    function tokenOfOwnerByIndex(address owner, uint256 index) external view returns (uint256 tokenId);

    /**
     * @dev Returns a token ID at a given `index` of all the tokens stored by the contract.
     * Use along with {totalSupply} to enumerate all tokens.
     */
    function tokenByIndex(uint256 index) external view returns (uint256);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "../../introspection/IERC165Upgradeable.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721Upgradeable is IERC165Upgradeable {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(address indexed from, address indexed to, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {safeTransferFrom} whenever possible.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool _approved) external;

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
      * @dev Safely transfers `tokenId` token from `from` to `to`.
      *
      * Requirements:
      *
      * - `from` cannot be the zero address.
      * - `to` cannot be the zero address.
      * - `tokenId` token must exist and be owned by `from`.
      * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
      * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
      *
      * Emits a {Transfer} event.
      */
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes calldata data) external;
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

import "./IERC721Upgradeable.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721MetadataUpgradeable is IERC721Upgradeable {

    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721ReceiverUpgradeable {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721.onERC721Received.selector`.
     */
    function onERC721Received(address operator, address from, uint256 tokenId, bytes calldata data) external returns (bytes4);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.7.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165Upgradeable {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}